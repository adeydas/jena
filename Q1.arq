# Query for related data
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX crm: <http://purl.org/NET/crm-owl#>
PREFIX claros: <http://purl.org/NET/Claros/vocab#>
PREFIX pf:  <http://jena.hpl.hp.com/ARQ/property#>
#SELECT ?g ?t (count(distinct ?s) as ?c) WHERE
SELECT count(*)
{  
   
  GRAPH ?g
  {
        {
          ?lit pf:textMatch ('athens' 5000) .
          { { ?ls rdf:value ?lit } UNION { ?ls rdfs:label ?lit } }
        }
      {
        {
          { 
            ?ls rdf:type crm:E35.Title .
            ?s crm:P102.has_title ?ls . 
          }
        UNION
          { 
            ?ls rdf:type crm:E55.Type . 
            ?s crm:P2.has_type ?ls .
          }
        UNION
          { 
            ?ls rdf:type crm:E42.Identifier . 
            ?s crm:P48.has_preferred_identifier ?ls .
          }
        UNION
          {
            {
              { LET ( ?pr := ?ls ) }
            UNION
              { 
                ?ls rdf:type crm:E57.Material . 
                ?pr crm:P126.employed ?ls . 
              }
            UNION
              { 
                ?ls rdf:type crm:E61.Time_Primitive .
                ?time crm:P82.at_some_time_within ?ls ; rdf:type crm:E52.Time-Span . 
                ?pr crm:P4.has_time-span ?time . 
              }
            }
            ?pr rdf:type crm:E12.Production .
            ?s crm:P108I.was_produced_by ?pr . 
          }
        UNION
          { 
            ?ls rdf:type crm:E62.String . 
            ?feature crm:P3.has_note ?ls ; rdf:type crm:E25.Man-Made_Feature .
            ?addition crm:P111.added ?feature ; rdf:type crm:E79.Part_Addition . 
            ?s crm:P111I.was_added_by ?addition .
          }
        UNION
          { 
            ?ls rdf:type crm:E55.Type . 
            ?assignment crm:P42.assigned ?ls ; rdf:type crm:E17.Type_Assignment .
            ?s crm:P14I.was_classified_by ?assignment .
          }
        UNION
          { 
            ?ls rdf:type crm:E53.Place . 
            ?s crm:P53.has_former_or_current_location ?ls .
          }
        UNION
          { 
            ?ls rdf:type crm:E38.Image . 
            ?s crm:P138I.has_representation ?ls .
          }
        UNION
          { 
            ?ls rdf:type crm:E31.Document . 
            ?s crm:P67I.is_referred_to_by ?ls .
          }
        UNION
          {
            {
              { 
                ?ls rdf:type crm:E48.Place_Name .  
                ?pl crm:P87.is_identified_by ?ls ; rdf:type crm:E53.Place .
              }
            UNION
              { 
                ?ls rdf:type crm:E48.Place_Name .  
                ?place crm:P87.is_identified_by ?ls ; rdf:type crm:E53.Place .
                ?pl crm:P89I.contains ?place .
              }
            }
            ?s crm:P53.has_former_or_current_location ?pl .
          }
        UNION
          { 
            ?ls rdf:type crm:E41.Appellation . 
            ?material crm:P1.is_identified_by ?ls ; rdf:type crm:E57.Material .
            ?s crm:P45.consists_of ?material .
          }
        }
      UNION
        { 
          ?ls rdf:type crm:E82.Actor_Appellation . 
          ?s crm:P131.is_identified_by ?ls ; rdf:type crm:E21.Person .
        }
      UNION
        { 
          ?ls rdf:type crm:E48.Place_Name .  
          ?s crm:P87.is_identified_by ?ls ; rdf:type crm:E53.Place .
        }
      }
    ?s rdf:type ?t .
  }
}
