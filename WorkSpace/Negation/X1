

Same data for every query:

# Data: the same for all examples : one triple: -------------------------------------

<http://example/a> <http://example/b> <http://example/c> .

An intersting query is Q2:

{ ?s ?p ?o } MINUS { :a :b :c }
{ ?s ?p ?o } NOT EXISTS { :a :b :c }

and 

	Andy


# Query 1/NOT EXISTS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     NOT EXISTS
  7       { ?s ?p ?o}
  8   }
# Results 
-------------
| s | p | o |
=============
-------------

# Query 1/MINUS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     MINUS
  7       { ?s ?p ?o}
  8   }
# Results 
-------------
| s | p | o |
=============
-------------


# Query 2/NOT EXISTS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     NOT EXISTS
  7       { :a :b :c}
  8   }
# Results
-------------
| s | p | o |
=============
-------------

# Query 2/MINUS -------------------------------------

  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     MINUS
  7       { :a :b :c}
  8   }
# Results
----------------
| s  | p  | o  |
================
| :a | :b | :c |
----------------


# Query 3/NOT EXISTS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     NOT EXISTS
  7       { ?s :b :c}
  8   }

-------------
| s | p | o |
=============
-------------

# Query 3/MINUS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     MINUS
  7       { ?s :b :c}
  8   }

-------------
| s | p | o |
=============
-------------

# Query 4/NOT EXISTS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     NOT EXISTS
  7       { ?x ?y ?z}
  8   }
# Results
-------------
| s | p | o |
=============
-------------

# Query 4/MINUS -------------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     MINUS
  7       { ?x ?y ?z}
  8   }
# Results
----------------
| s  | p  | o  |
================
| :a | :b | :c |
----------------

# Query 5/NOT EXISTS -------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     NOT EXISTS
  7       { ?s :b :c
  8         FILTER ( ?s = :a )
  9       }
 10   }

-------------
| s | p | o |
=============
-------------

# Query 5/MINUS -------------------------------
  1 PREFIX  :     <http://example/>
  2 
  3 SELECT  *
  4 WHERE
  5   { ?s ?p ?o
  6     MINUS
  7       { ?s :b :c
  8         FILTER ( ?s = :a )
  9       }
 10   }

-------------
| s | p | o |
=============
-------------

