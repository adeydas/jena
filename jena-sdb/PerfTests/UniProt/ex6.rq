# ==== Ex 6 : Genes associated with human diseases
# 
# SELECT 
#   ?gene AS "Gene", ?name AS "Name", ?text AS "Disease"
# WHERE 
#   [?protein rdf:type :Protein] AND
#   [?protein :gene ?gene] AND
#   [?gene :name ?name] AND 
#   [?protein :organism taxon:9606] AND 
#   [?protein :annotation ?annotation] AND 
#   [?annotation rdf:type :Disease_Annotation] AND 
#   [?annotation rdfs:comment ?text] 
# USING 
#   "urn:lsid:uniprot.org:ontology:",
#   "http://www.w3.org/1999/02/22-rdf-syntax-ns#" AS rdf, 
#   "http://www.w3.org/2000/01/rdf-schema#" AS rdfs,
#   "urn:lsid:uniprot.org:taxonomy:" AS taxon

PREFIX :      <urn:lsid:uniprot.org:ontology:>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX taxon: <urn:lsid:uniprot.org:taxonomy:>

# No variable aliases
SELECT ?gene ?name ?text
{
  ?protein rdf:type :Protein .
  ?protein :gene ?gene .
  ?gene :name ?name .
  ?protein :organism <urn:lsid:uniprot.org:taxonomy:9606> .
  ?protein :annotation ?annotation .
  ?annotation rdf:type :Disease_Annotation .
  ?annotation rdfs:comment ?text .
}
